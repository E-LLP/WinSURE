(* QUAD WITH TRANSIENT FAULTS *)

NP = 4;                 (* Number of processors *)
LAMBDA_P = 1E-4;        (* Permanent fault arrival rate *)
LAMBDA_T = 10*LAMBDA_P; (* Transient fault arrival rate *)
DELTA_D = .5;           (* Transient fault disappearance rate *)
DELTA_R = 3.6E3;        (* Reconfiguration rate *)

SPACE = (NW: 0..NP,     (* Number of working processors *)
         NFP: 0..NP,    (* Active procs. with permanent faults *)
         NFT: 0..NP);   (* Active procs. with transient faults *)
START = (NP, 0, 0);

DEATHIF NFP+NFT >= NW;  (* Majority of active processors failed *)

IF NW>0 THEN
   TRANTO (NW-1,NFP+1,) BY NW*LAMBDA_P; (* Permanent fault arrival *)
   TRANTO (NW-1,,NFT+1) BY NW*LAMBDA_T; (* Transient fault arrival *)
ENDIF;

IF NFT > 0 THEN
   TRANTO (NW+1,,NFT-1) BY FAST DELTA_D;(* Transient fault disappearance *)
   TRANTO NFT-- BY FAST DELTA_R;        (* Transient fault reconfiguration *)
ENDIF;

IF NFP > 0 TRANTO NFP-- BY FAST DELTA_R;(* Permanent fault reconfiguaration *)
