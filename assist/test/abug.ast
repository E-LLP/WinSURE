NSI=1;                (* number of spares initially, can be anything *)
SPACE = (N1: 0..3,    (* number of processors in first triad *)
         N2: 0..3,    (* number of processors in second triad *)
         N3: 0..3,    (* number of processors in second triad *)
         F1: 0..2,    (* number of faulty processors in first triad *)
         F2: 0..2,    (* number of fault processors in second triad *)
         F3: 0..2,    (* number of fault processors in second triad *)
         NS);         (* number of spares *)

START = (3,3,3,0,0,0,NSI);

LAMBDA = 5E-4;  (* failure rate of active processors *)
GAMMA = 5E-4;   (* failure rate of spares *)
MU = .400/3600  (* rate at which faulty processors are removed *)
STD = MU;

DEATHIF (F1 >= N1) OR (F2 >= N2) OR (F3 >= N3);

IF N1 > 0 THEN TRANTO F1=F1+1, N1=N1-1 BY N1*LAMBDA;
IF N2 > 0 THEN TRANTO F2=F2+1, N2=N2-1 BY N2*LAMBDA;
IF N3 > 0 THEN TRANTO F3=F3+1, N3=N3-1 BY N3*LAMBDA;

IF NS > 0 THEN TRANTO NS=NS-1 BY NS*GAMMA;

IF (F1 > 0) AND (NS=0) THEN TRANTO ( 1,N2,N3, 0,F2,F3,NS+1) BY 
    <MU,STD,1/(F1+F2+F3)>;
IF (F2 > 0) AND (NS=0) THEN TRANTO (N1, 1,N3,F1, 0,F3,NS+1) BY 
    <MU,STD,1/(F1+F2+F3)>;
IF (F3 > 0) AND (NS=0) THEN TRANTO (N1,N2, 1,F1,F2, 0,NS+1) BY 
    <MU,STD,1/(F1+F2+F3)>;
IF (F1 > 0) AND (NS > 0) THEN 
   TRANTO F1=F1-1, N1=N1+1, NS=NS-1) 
   BY <MU,STD,1/(F1+F2+F3)>;
IF (F2 > 0) AND (NS > 0) THEN 
   TRANTO F2=F2-1, N2=N2+1, NS=NS-1) 
   BY  <MU,STD,1/(F1+F2+F3)>;
IF (F3 > 0) AND (NS > 0) THEN 
   TRANTO F3=F3-1, N3=N3+1, NS=NS-1) 
   BY  <MU,STD,1/(F1+F2+F3)>;

