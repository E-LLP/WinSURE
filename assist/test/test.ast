(* MYPIT.AST *)
(* THIS MODEL INSTANEOUSLY RECONFIGURES SENSORS UPON NODE FAILURE *)

ECHO=0;
COMMENT=0;
TIME=3.0;
LIST=2;
(*PRUNE=1E-15;*)


LAMSEN = 10.0E-6;            (* SENSOR FAILURE RATES *)
LAMCOM = 15.0E-6;	     (* NETWORK NODE FAILURE RATE *)
LAMOUT = 56.1E-7;	     (* NETWORK TEMPORARY OUTAGE RATE *)
LAMDIU = 15.0E-6;	     (* DIU FAILURE RATE *)
LAMELEC = 20.0E-6;	     (* PRIMARY ELECTRICAL SYSTEM FAIL RATE *)
RECMEAN = 3.0E-4;	     (* SENSOR RECOVERY TIME MEAN *)
RECSTD = 1.0E-4;	     (* SENSOR RECOVERY TIME STD DEV *)

(* LET PARTS[1] HOLD NODES 1,2 AND PARTS[2] HOLD NODES 3,4 *)
(* AND SENSOR[I] IS ATTACHED TO NODE [I]  *)

SPACE = (PARTS: ARRAY[1..2] OF 0..1, (* PARTITIONS *)
         NODES: ARRAY[1..4] OF 0..1, (* LOW LEVEL NODES *)
         NGPIT: ARRAY[1..4] OF 0..1, (* NO. OF GOOD PITCH *)
         NFPIT: ARRAY[1..4] OF 0..2,  (* NO. OF RECOVERING PITCH SENSORS *)
         NCF);              (* NO. OF COMPONENT FAILURES FOR PRUNING *)

START = (2 OF 1, 4 OF 1, 4 OF 1, 4 OF 0 , 0);

(* MAJORITY VOTER *)
DEATHIF (NGPIT[1] + NGPIT[2] + NGPIT[3] + NGPIT[4]) <=
        (NFPIT[1] + NFPIT[2] + NFPIT[3] + NFPIT[4]);

(* EXHAUSTION OF SENSORS *)
DEATHIF NGPIT[1] + NGPIT[2] + NGPIT[3] + NGPIT[4] < 1;

(* INTERACTION OF SENSORS AND NETWORK *)
DEATHIF (PARTS[1] = 0) AND 
        (NGPIT[3] + NGPIT[4] - NFPIT[3] - NFPIT[4] <= 0);

DEATHIF (PARTS[2] = 0) AND
        (NGPIT[1] + NGPIT[2] - NFPIT[1] - NFPIT[2] <= 0);

(* EXHAUSTION OF PARTITIONS *)
DEATHIF (PARTS[1] + PARTS[2]) < 1;

ONEDEATH=1;

PRUNEIF NFPIT[1]+NFPIT[2]+NFPIT[3]+NFPIT[4] + NCF > 2;
PRUNEIF NCF >= 3; (* PRUNE AT THE 3 COMPONENT FAILURE LEVEL *)

FOR I = 1,4;
   IF NGPIT[I] = 1 TRANTO NGPIT[I] = 0, NCF = NCF + 1,
                               NFPIT[I] = 1 BY LAMSEN;

   IF NODES[I] =1 THEN
      IF I=1 OR I=2     TRANTO NODES[I] = 0, NCF=NCF+1,
    			NGPIT[I] = 0, NFPIT[I] = 0,
                        PARTS[1] = 0
                        BY (LAMCOM + LAMDIU + LAMELEC);

      IF I=3 OR I=4     TRANTO NODES[I] = 0, NCF=NCF+1,
    			NGPIT[I] = 0, NFPIT[I] = 0,
                        PARTS[2] = 0
                        BY (LAMCOM + LAMDIU + LAMELEC);
ENDIF;

   IF NFPIT[I] = 1 TRANTO NFPIT[I] = 0 BY FAST 1.0/RECMEAN;


ENDFOR;

FOR J = 1,2;
    IF PARTS[J] = 1 TRANTO PARTS[J] = 0 BY LAMOUT;
    IF PARTS[J] = 0 TRANTO PARTS[J] = 1 BY FAST 1.0/RECMEAN;
ENDFOR;

