(* 2 TRIADS OF COMPUTERS WITH 1 TRIAD OF POWER SUPPLIES        *)
(* CONNECTED SUCH THAT 1 COMPUTER IN EACH TRIAD IS CONNECTED TO  *)
(* EACH POWER SUPPLY.  THUS, IF A POWER SUPPLY FAILS, THEN ONE *)
(* COMPUTER IN EACH TRIAD FAILS.  THE SYSTEM FAILS IF EITHER   *)
(* TRIAD OF COMPUTERS FAILS.                                   *)

LAM_PS = 1E-6;	(* Failure rate of power supplies *)
LAM_C = 1E-5;	(* Failure rate of computers *)

SPACE = (CAF: ARRAY[1..3] OF 0..1,	(* Failed computers in Triad A *)
         CBF: ARRAY[1..3] OF 0..1,	(* Failed computers in Triad B *)
         PSF: ARRAY[1..3] OF 0..1);	(* Failed power supplies *)
START = (9 OF 0);

DEATHIF CAF[1] + CAF[2] + CAF[3] > 1;
	(* 2/3 computers in Triad A failed *)
DEATHIF CBF[1] + CBF[2] + CBF[3] > 1;
	(* 2/3 computers in Triad B failed *)

FOR I = 1,3;
   IF CAF[I]=0 TRANTO CAF[I] = 1 BY LAM_C;
   	(* Failure of computer in Triad A *)

   IF CBF[I]=0 TRANTO CBF[I] = 1 BY LAM_C;
   	(* Failure of computer in Triad B *)

   IF PSF[I]=0 TRANTO CAF[I] = 1, CBF[I] = 1, PSF[I] = 1 BY LAM_PS;
   	(* Power supply failure *)
ENDFOR;

