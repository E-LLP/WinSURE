(* MONITORED SENSOR FAILURE MODEL *)

LAMBDA_S = 10E-6;	(* Failure rate of sensors *)
LAMBDA_M = 1E-6;	(* Failure rate of monitors *)
MEAN_1 = 3E-4;		(* Mean recovery time for first fault *)
SD_1 = 1E-4;		(* S.D. of recovery time for first fault *)
MEAN_2 = 1E-4;		(* Mean recovery time for second fault *)
SD_2 = 2E-5;		(* S.D. of recovery time for second fault *)
COV_2 = .98;		(* Coverage for second failure *)

SPACE = (NS: 0..3,	(* Number of active sensors *)
         NFS: 0..3,	(* Number of failed active sensors *)
         NM: 0..3);	(* Number of working monitors *)
START = (3, 0, 3);

DEATHIF 2*NFS > NS;	(* Majority of sensors failed *)
DEATHIF 2*NFS = NS AND NM < NS;
   (* Half of the sensors fail and the number of monitors *)
   (*   working is less than the number of active sensors *)

IF NS>0 TRANTO NFS = NFS+1 BY (NS-NFS)*LAMBDA_S;	(* Sensor failure *)
IF NM>1 TRANTO NM = NM-1 BY NM*LAMBDA_M;		(* Monitor failure *)

   (* First fault recovery *)
IF NS>2*NFS AND NFS>0 THEN
   IF NM>0 TRANTO (NS-1, NFS-1, NM-1) BY <MEAN_1,SD_1,(NM/NS)>;
   	(* Loss of monitored sensor *)
   IF NM<NS TRANTO (NS-1, NFS-1, NM) BY <MEAN_1,SD_1,(NS-NM)/NS>;
   	(* Loss of unmonitored sensor *)
ENDIF;

   (* Second fault recovery *)
IF NS=2*NFS AND NFS>0 THEN
   TRANTO (NS-1, NFS-1, NM) BY <MEAN_2,SD_2,COV_2>;
   	(* Successfully removed failed sensor *)
   TRANTO (NS-1,NFS,NM) BY <MEAN_2,SD_2,1.0-COV_2>;
   	(* Mistakenly removed nonfaulty sensor *)
ENDIF;
